plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'org.jetbrains.kotlin.plugin.parcelize'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
    id 'androidx.navigation.safeargs.kotlin'
    id 'com.github.triplet.play'
}

// Apply Google Services and Crashlytics only if config files exist
def googleServicesFile = file("google-services.json")
if (googleServicesFile.exists()) {
    apply plugin: 'com.google.gms.google-services'
    apply plugin: 'com.google.firebase.crashlytics'
}

apply from: "$rootDir/gradle/helpers/common-methods.gradle"

Properties semver = new Properties()
semver.load(new FileInputStream(file("semver.properties")))

def buildTimeMillis = System.currentTimeMillis()
def buildVersionCode = computeVersionCode(semver['major'], semver['minor'], semver['patch'], semver['build'])
def buildVersionName = computeVersionName(semver['major'], semver['minor'], semver['patch'])
def buildTime = computeBuildTimeFormatted(buildTimeMillis)
def buildRevision = computeRevision()

ant.propertyfile(file: "version.properties") {
    entry(key: "version", value: buildVersionName)
    entry(key: "versionCode", value: buildVersionCode)
    entry(key: "rev", value: buildRevision)
    entry(key: "buildTime", value: buildTime)
}

android {
    namespace "com.kholhang.kcclabu"
    compileSdk versions.targetSdk

    defaultConfig {
        applicationId "com.kholhang.kcclabu"
        minSdk versions.minSdk
        targetSdk versions.targetSdk
        versionCode buildVersionCode
        versionName buildVersionName
        vectorDrawables.useSupportLibrary = true

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    def keystorePropertiesFile = file("../app/keystore.properties")
    def keystoreProperties = new Properties()
    if (keystorePropertiesFile.exists()) {
        keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
    }

    signingConfigs {
        if (keystorePropertiesFile.exists()) {
            release {
                storeFile file(keystoreProperties['release.keystore'])
                storePassword keystoreProperties['release.keystore.password']
                keyAlias keystoreProperties['key.alias']
                keyPassword keystoreProperties['key.password']
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            if (keystorePropertiesFile.exists()) {
                signingConfig signingConfigs.release
            }
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
    buildFeatures {
        compose = true
        buildConfig = true
        viewBinding = true
    }
    composeOptions {
        kotlinCompilerExtensionVersion = versions.compose_compiler
    }
    packaging {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
    hilt {
        enableTransformForLocalTests = true
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:${versions.kotlin}"

    // Compose BOM
    implementation platform("androidx.compose:compose-bom:${versions.compose_bom}")
    implementation "androidx.compose.ui:ui"
    implementation "androidx.compose.ui:ui-graphics"
    implementation "androidx.compose.ui:ui-tooling-preview"
    implementation "androidx.compose.material3:material3"
    implementation "androidx.compose.material:material-icons-core"
    implementation "androidx.activity:activity-compose:1.9.2"
    implementation "androidx.lifecycle:lifecycle-viewmodel-compose:${versions.lifecycle}"
    implementation "androidx.lifecycle:lifecycle-runtime-compose:${versions.lifecycle}"
    implementation "androidx.compose.runtime:runtime-livedata"
    implementation "androidx.navigation:navigation-compose:${versions.navigation}"
    implementation "androidx.hilt:hilt-navigation-compose:${versions.hilt_androidx}"
    debugImplementation "androidx.compose.ui:ui-tooling"
    debugImplementation "androidx.compose.ui:ui-test-manifest"

    // Androidx
    implementation "androidx.core:core-ktx:${versions.ktx}"
    implementation "androidx.appcompat:appcompat:${versions.appcompat}"
    implementation "com.google.android.material:material:${versions.material}"
    implementation "androidx.constraintlayout:constraintlayout:${versions.const_layout}"
    implementation "androidx.recyclerview:recyclerview:${versions.recyclerview}"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:${versions.lifecycle}"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:${versions.lifecycle}"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:${versions.lifecycle}"
    implementation "androidx.lifecycle:lifecycle-common-java8:${versions.lifecycle}"
    implementation "androidx.room:room-runtime:${versions.room}"
    implementation "androidx.room:room-ktx:${versions.room}"
    kapt "androidx.room:room-compiler:${versions.room}"
    implementation "androidx.startup:startup-runtime:${versions.startup}"
    implementation "androidx.preference:preference-ktx:${versions.preference}"
    implementation "androidx.browser:browser:${versions.browser}"

    // Coroutines
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:${versions.coroutines}"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:${versions.coroutines}"

    // Dagger
    implementation "com.google.dagger:hilt-android:${versions.hilt}"
    kapt "com.google.dagger:hilt-android-compiler:${versions.hilt}"
    kapt "androidx.hilt:hilt-compiler:${versions.hilt_androidx}"

    // Firebase
    implementation "com.google.firebase:firebase-storage-ktx:${versions.fb_storage}"
    implementation "com.google.firebase:firebase-auth-ktx:${versions.fb_auth}"
    implementation "com.google.firebase:firebase-database-ktx:${versions.fb_database}"
    implementation "com.google.firebase:firebase-analytics-ktx:${versions.fb_analytics}"
    implementation "com.google.firebase:firebase-crashlytics:${versions.fb_crashytics}"

    implementation "com.squareup.moshi:moshi-kotlin:${versions.moshi}"
    kapt "com.squareup.moshi:moshi-kotlin-codegen:${versions.moshi}"

    implementation "com.jakewharton.timber:timber:${versions.timber}"
    implementation "com.android.billingclient:billing-ktx:${versions.billing}"
    // Aztec editor - commented out due to dependency resolution issues
    // implementation "com.github.wordpress-mobile.WordPress-Aztec-Android:aztec:${versions.aztec}"

    testImplementation "junit:junit:$versions.junit"
    testImplementation "org.mockito:mockito-core:$versions.mockito"
    testImplementation "androidx.arch.core:core-testing:$versions.androidx_testing"
    testImplementation "org.amshove.kluent:kluent-android:$versions.kfluent"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:${versions.coroutines}"
    testImplementation "androidx.test.ext:junit:$versions.androidx_junit"
    testImplementation "org.robolectric:robolectric:$versions.robolectric"
    androidTestImplementation "androidx.test.ext:junit:$versions.androidx_junit"
    androidTestImplementation "androidx.test.espresso:espresso-core:$versions.espresso"
    androidTestImplementation platform("androidx.compose:compose-bom:${versions.compose_bom}")
    androidTestImplementation "androidx.compose.ui:ui-test-junit4"

}

play {
    serviceAccountCredentials = file("play-key.json")
    track = "alpha"
    fromTrack = "alpha"
    promoteTrack = "beta"
    defaultToAppBundles = true
    resolutionStrategy = com.github.triplet.gradle.androidpublisher.ResolutionStrategy.AUTO
}